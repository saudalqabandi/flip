module Particle
   use Config
   use Utils

   type :: Particles
      integer :: nParticles, nCells
      real:: lBox, l, pressure, eta, w0, kappa, rCut, beta, drMax, dvMax, lambda, v0, rho, vOld, potential
      logical :: over
      real, allocatable :: r(:, :), u(:, :), q(:)
      character(len=50) :: setup
   end type Particles

contains

   function initParticles(cfg) result(p)

      type(ConfigFile), intent(in) :: cfg
      type(Particles) :: p

      real :: pi, third, lBoxMin

      pi = 4.0*atan(1.0)
      third = 1.0/3.0
      lBoxMin = 2*(cfg%l + 1)

      ! assign values from config struct
      p%nParticles = cfg%nParticles
      p%l = cfg%l
      p%pressure = cfg%pressure
      p%eta = cfg%eta
      p%w0 = cfg%w0
      p%kappa = cfg%kappa
      p%rCut = cfg%rCut
      p%beta = cfg%beta
      p%drMax = cfg%drMax
      p%dvMax = cfg%dvMax
      p%lambda = cfg%lambda
      p%setup = cfg%setup
      p%q = cfg%q

      ! calculate derived values
      p%v0 = (pi/6.0) + 0.25*pi*p%l
      p%rho = p%eta/p%v0
      p%vOld = p%nparticles/p%rho
      p%lBox = (p%vOld)**third

      p%over = .false.

      ! setup the particles
      print *, 'Setting up particles...'
      call setupParticles(p)

   end function initParticles

   subroutine setupParticles(p)
      type(Particles), intent(inout) :: p
      if (p%setup == 'random') then
         print *, 'Setting up random lattice'
         call setupRandom(p)
      else if (p%setup == 'cubic') then
         print *, 'Setting up cubic lattice'
         call setupCubic(p)
      else
         print *, 'Invalid setup type: ', p%setup
         stop
      end if
   end subroutine setupParticles

   subroutine setupRandom(p)
      type(Particles), intent(inout) :: p

      integer :: i, j
      real, dimension(3) :: vec

      ! allocate arrays
      allocate (p%r(p%nParticles, 3))
      allocate (p%u(p%nParticles, 3))

      do i = 1, p%nParticles
         do j = 1, 3
            p%r(i, j) = p%lBox*ranNum()
         end do
         call ranVec(vec)
         p%u(i, :) = vec
      end do
   end subroutine setupRandom

   subroutine setupCubic(p)

      real :: third, step, halfBox, x, y, z
      integer :: i, j, k, n, index
      real, dimension(3) :: vec

      type(Particles), intent(inout) :: p

      third = 1.0/3.0
      n = nint(p%nParticles**third)

      if (n**3 /= p%nParticles) then
         print *, "Number of particles is not a perfect cube, adjusting from ", p%nParticles, " to ", n**3
         p%nParticles = n**3
         p%vOld = p%nParticles/p%rho
         p%lBox = (p%vOld)**third
      end if

      ! allocate arrays
      allocate (p%r(p%nParticles, 3))
      allocate (p%u(p%nParticles, 3))

      step = p%lBox/n
      halfBox = p%lBox/2.0

      index = 1
      do i = 1, n
         do j = 1, n
            do k = 1, n
               x = -halfBox + (i - 0.5)*step
               y = -halfBox + (j - 0.5)*step
               z = -halfBox + (k - 0.5)*step

               p%r(index, :) = (/x, y, z/)
               index = index + 1
            end do
         end do
      end do

      do i = 1, p%nParticles
         call ranVec(vec)
         p%u(i, :) = vec
      end do
   end subroutine setupCubic

   subroutine saveVTK(p, cfg, step)

      type(Particles), intent(in) :: p
      type(ConfigFile), intent(in) :: cfg

      integer, intent(in) :: step
      character(len=50) :: stepChar, nChar, lChar, etaChar, fileName
      character(len=100) :: fullPath
      integer :: ioStatus

      ! Convert integer i to character string
      write (stepChar, '(I0)') step
      write (nChar, '(I0)') cfg%nParticles
      write (lChar, '(F0.2)') cfg%l
      write (etaChar, '(F0.2)') cfg%eta

      fileName = trim(cfg%fileName)//'_'//trim(stepChar)//'.vtk'
      fullPath = 'output/'//trim(cfg%dirName)//'/'//trim(fileName)

      print *, 'Saving VTK file: ', fileName
      print *, 'Output Directory: ', 'output/'//trim(cfg%dirName)
      open (unit=10, file=fullPath, status='replace', action='write', iostat=ioStatus)
      if (ioStatus /= 0) then
         print *, 'Error opening file:', fullPath
         stop
      end if

      write (10, '(A)') '# vtk DataFile Version 3.0'
      write (10, '(A)') 'VTK file generated by MC flip program'
      write (10, '(A)') 'ASCII'
      write (10, '(A)') 'DATASET UNSTRUCTURED_GRID'
      write (10, '(A)') 'POINTS '//trim(adjustl(nChar))//' float'
      do i = 1, p%nParticles
         write (10, '(3F10.5)') p%r(i, 1), p%r(i, 2), p%r(i, 3)
      end do
      write (10, '(A)') 'POINT_DATA '//trim(adjustl(nChar))
      write (10, '(A)') 'VECTORS orientation float'
      do i = 1, p%nParticles
         write (10, '(3F10.5)') p%u(i, 1), p%u(i, 2), p%u(i, 3)
      end do

      close (10)

   end subroutine saveVTK

end module Particle
